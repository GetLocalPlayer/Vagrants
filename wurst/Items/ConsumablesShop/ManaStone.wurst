package ManaStone
import Assets
import ObjectIds
import ObjectIdGenerator
import AbilityObjEditing
import ConsumablesShop
import RunedAlias
import ItemTooltipGenerator
import ItemAbilityPreset

import EventHelper


constant int GOLD_COST = 60
constant CLASSIFICATION = "Charged"
constant NAME = "Mana Stone"
constant ICON = Icons.bTNManaStone
constant int SHOP_X = 1
constant int SHOP_Y = 0
constant real MANA_REG = 0.2
constant int MANA_RESTORING = 180
constant real COOLDOWN = 30
constant ACTIVE_DESCRIPTION = "Can be consumed to restore {0} mana points ({1} sec. cooldown).".format(MANA_RESTORING.toString(), COOLDOWN.toInt().toString())

constant ITEM_ID = compiletime(ITEM_ID_GEN.next())


@compiletime function build()
	let tooltipGenerator = new ItemTooltipGenerator(NAME, "", GOLD_COST, "")
	..setActiveEffect(ACTIVE_DESCRIPTION)
	let manaRegDef = buildManaRegenerationBonus(NAME, ICON, MANA_REG, tooltipGenerator)
	let manaRestDef = new AbilityDefinitionItemManaRestoreGreater(ABIL_ID_GEN.next())
	..setRace(ITEM_ABILITY_RACE)
	..setName(NAME)
	..setIconNormal(ICON)
	..setEditorSuffix("Mana Restoration ({0})".format(MANA_RESTORING.toString()))
	..setLevels(1)
	..setManaPointsGained(1, MANA_RESTORING)
	..setCooldown(1, COOLDOWN)
	buildItemWithAlias(ITEM_ID) (def, isAlias) ->
		def..setLevel(1)
		..setLevelUnclassified(1)
		..setLumberCost(0)
		..setHitPoints(999999)
		..setNumberofCharges(0)
		..setStockMaximum(1)
		..setStockReplenishInterval(1)
		..setStockStartDelay(1)
		..setNumberofCharges(1)
		..setCanBeDropped(true)
		..setPerishable(true)
		..setActivelyUsed(true)
		..setCanBeSoldByMerchants(true)
		..setCanBeSoldToMerchants(true)
		..setGoldCost(GOLD_COST)
		..setButtonPositionX(SHOP_X)
		..setButtonPositionY(SHOP_Y)
		..setClassification(CLASSIFICATION)
		..setInterfaceIcon(ICON)
		..setCooldownGroup(manaRestDef.getNewId().toRawCode())
		tooltipGenerator.applyToDef(def)
		if isAlias
			consumablesShop.addItemSold(def.getNewId())
		else
			def.setAbilities("{0},{1}".format(manaRestDef.getNewId().toRawCode(), manaRegDef.getNewId().toRawCode()))


init
	onAliasedItemGiven() (unit u, item givenItem) ->
		if givenItem.getTypeId() == ITEM_ID
			for i in EventData.getTriggerUnit().inventory()
				if i.getTypeId() == ITEM_ID and i != givenItem
					let charges = givenItem.getCharges()
					givenItem.remove()
					i.setCharges(i.getCharges() + charges)
					break